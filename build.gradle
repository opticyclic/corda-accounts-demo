buildscript {
  ext {
    corda_release_version = '4.1'
    corda_gradle_plugins_version = '4.0.42'
    quasar_version = '0.7.10'
    kotlin_version = '1.2.71'
    testng_version = '6.14.3'
  }

  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
    classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
  }
}

allprojects {
  repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
    //Crash Shell dependencies are not published correctly in R3 repo
    maven { url 'https://jitpack.io' }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      languageVersion = "1.2"
      apiVersion = "1.2"
      jvmTarget = "1.8"
      javaParameters = true // Useful for reflection.
    }
  }

  test {
    // enable TestNG support (default is JUnit)
    useTestNG() {
      useDefaultListeners = true
    }
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'

dependencies {
  cordaCompile "net.corda:corda-node-api:$corda_release_version"

  // CorDapp dependencies.
  cordapp project(":contracts")
  cordapp project(":workflows")
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
  nodeDefaults {
    projectCordapp {
      deploy = false
    }
    cordapp project(':contracts')
    cordapp project(':workflows')
    rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
  }
  node {
    name "O=Notary,L=London,C=GB"
    notary = [validating: false]
    p2pPort 10001
    rpcSettings {
      address("localhost:10002")
      adminAddress("localhost:10003")
    }
    cordapps.clear()
  }
  node {
    name "O=Bank1,L=London,C=GB"
    p2pPort 10004
    rpcSettings {
      address("localhost:10005")
      adminAddress("localhost:10006")
    }
  }
  node {
    name "O=Bank2,L=London,C=GB"
    p2pPort 10007
    rpcSettings {
      address("localhost:10008")
      adminAddress("localhost:10009")
    }
  }
  node {
    name "O=Agent1,L=London,C=GB"
    p2pPort 10010
    rpcSettings {
      address("localhost:10011")
      adminAddress("localhost:10012")
    }
  }
  node {
    name "O=Agent2,L=London,C=GB"
    p2pPort 10013
    rpcSettings {
      address("localhost:10014")
      adminAddress("localhost:10015")
    }
  }
}
